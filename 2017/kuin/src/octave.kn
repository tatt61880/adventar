{※生成したwavファイルを再生する際は、音量にご注意ください。}
var title: []char :: "Kuinで作る、はじめてのDAW制作"
var wndMain: wnd@Wnd 
const width: int :: 400
const height: int :: 120
var inputEdit: wnd@Edit
var tabOrder: wnd@TabOrder
var sinRadio: wnd@Radio
var rectRadio: wnd@Radio
var chordsChk: wnd@Chk
var vibratoChk: wnd@Chk
var saveBtn: wnd@Btn
var resetBtn: wnd@Btn
const defaultInputStr: []char :: "19 18 19 18 19 14 17 15 12 12 12 3 7 12 14 14 14 7 11 14 15 15 15"
;const defaultInputStr: []char :: "10 12 19 17 15 17 15 17 19 15 10 15 17 12 14 15 12 14 15 17 10 15 17 19"

func main()
	do @wndMain :: wnd@makeWnd(null, %aspect, @width, @height, @title)
	do wnd@makeLabel(@wndMain, 5, 5, 290, 12, %fix, %fix, "音を数値で入力してください。スペース区切りです。")
	do @inputEdit :: wnd@makeEdit(@wndMain, 5, 30, @width - 10, 20, %scale, %fix)
	do @sinRadio :: wnd@makeRadio(@wndMain, 5, 60, 60, 20, %fix, %fix, "サイン波")
	do @sinRadio.setChk(true)
	do @rectRadio :: wnd@makeRadio(@wndMain, 80, 60, 60, 20, %fix, %fix, "矩形波")
	do @vibratoChk :: wnd@makeChk(@wndMain, 160, 60, 80, 20, %fix, %fix, "ビブラート")
	do @chordsChk :: wnd@makeChk(@wndMain, 240, 60, 80, 20, %fix, %fix, "和音")
	do @saveBtn :: wnd@makeBtn(@wndMain, 5, 90, 50, 20, %fix, %fix, "保存")
	do @resetBtn :: wnd@makeBtn(@wndMain, @width - 55, 90, 50, 20, %move, %fix, "リセット")
	do @resetBtn.onPush :: reset
	do @saveBtn.onPush :: save
	do @tabOrder :: wnd@makeTabOrder([@inputEdit $ wnd@WndBase, @saveBtn $ wnd@WndBase, @resetBtn $ wnd@WndBase])
	do wnd@onKeyPress(@onKeyPress)
	do reset(null)
	while(wnd@act())
	end while

	func reset(wnd: wnd@WndBase)
		do @inputEdit.setText(@defaultInputStr)
	end func

	func save(wnd: wnd@WndBase)
		var path: []char :: wnd@saveFileDialog(@wndMain, ["音声データ (*.wav)", "*.wav"], 0, "wav")
		if(path =& null)
			ret
		end if
		var filePtr: file@Writer :: file@makeWriter(path, false)
		if(filePtr =& null)
			do wnd@msgBox(@wndMain, "保存に失敗しました。\{path}", @title, %err, %ok)
			ret
		end if

		var inputStr: []char :: @inputEdit.getText()
		var splittedStrs: [][]char :: inputStr.split(" ")
		var datasize: int :: ^splittedStrs * 44100
		var filesize: int :: datasize + 44 {ヘッダが44bytes}
		var header: []bit8 :: [
		| 16#52b8, 16#49b8, 16#46b8, 16#46b8] {'RIFF'}
		| ~ ((filesize - 8) $> []bit8).sub(0, 4) ~ {ファイルサイズ(byte) - 8(byte)}
		| [
		| 16#57b8, 16#41b8, 16#56b8, 16#45b8, {'WAVE'}
		| 16#66b8, 16#6Db8, 16#74b8, 16#20b8, {'fmt '}
		| 16#10b8, 16#00b8, 16#00b8, 16#00b8, {fmt チャンクのバイト数}
		| 16#01b8, 16#00b8, {フォーマットID}
		| 16#01b8, 16#00b8, {チャンネル数}
		| 16#44b8, 16#ACb8, 16#00b8, 16#00b8, {サンプリングレート}
		| 16#44b8, 16#ACb8, 16#00b8, 16#00b8, {データ速度}
		| 16#01b8, 16#00b8, {ブロックサイズ}
		| 16#08b8, 16#00b8, {サンプルあたりのビット数}
		| 16#64b8, 16#61b8, 16#74b8, 16#61b8] {'data'}
		| ~ ((datasize - 8) $> []bit8).sub(0, 4)  {データサイズ(byte)}
		var bin: []bit8 :: ##header
		var theta1: float :: 0.0
		var theta2: float :: 0.0
		var vib: float :: 0.0
		for i(0, ^splittedStrs - 1)
			var key: float
			do splittedStrs[i].toFloat(&key)
			for j(0, 44100 / 4)
				if(@chordsChk.getChk())
					do bin :~ [(
					| (@sinRadio.getChk() ?(lib@sin(theta1), lib@sin(theta1) > 0.0 ?(1.0, -1.0))) * 64.0 +
					| (@sinRadio.getChk() ?(lib@sin(theta2), lib@sin(theta2) > 0.0 ?(1.0, -1.0))) * 63.0 +
					| 128.0) $ int $ bit8]
				else
					do bin :~ [((@sinRadio.getChk() ?(lib@sin(theta1), lib@sin(theta1) > 0.0 ?(1.0, -1.0))) * 127.0 + 128.0) $ int $ bit8]
				end if
				do theta1 :+ (441.0 + (@vibratoChk.getChk() ?(lib@sin(vib) * 10.0, 0.0))) * 2.0 ^ ((key + 0.0) / 12.0) * 2.0 * lib@pi / 44100.0
				do theta2 :+ (441.0 + (@vibratoChk.getChk() ?(lib@sin(vib) * 10.0, 0.0))) * 2.0 ^ ((key + 7.0) / 12.0) * 2.0 * lib@pi / 44100.0
				do vib :+ 5.0 * 2.0 * lib@pi / 44100.0
			end for
		end for
		do filePtr.write(bin)
		do filePtr.fin()
		do wnd@msgBox(@wndMain, "保存しました。", @title, %none, %ok)
	end func

	func saveWav(bin: []bit8): bool
	end func
end func

func onKeyPress(key: wnd@Key, shiftCtrl: wnd@ShiftCtrl): bool
	if(@tabOrder <>& null & @tabOrder.chk(key, shiftCtrl))
		ret false
	end if
	ret false
end func
