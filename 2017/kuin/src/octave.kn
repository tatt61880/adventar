var title: []char :: "Kuinで作る、はじめてのDAW制作"
var wndMain: wnd@Wnd 
const width: int :: 400
const height: int :: 100
var inputEdit: wnd@Edit
var tabOrder: wnd@TabOrder
var saveBtn: wnd@Btn
var resetBtn: wnd@Btn
const defaultInputStr: []char :: "19 18 19 18 19 14 17 15 12 12 12 3 7 12 14 14 14 7 11 14 15 15 15"

func main()
	do @wndMain :: wnd@makeWnd(null, %aspect, @width, @height, @title)
	do wnd@makeLabel(@wndMain, 5, 5, 290, 12, %fix, %fix, "音を数値で入力してください。スペース区切りです。")
	do @inputEdit :: wnd@makeEdit(@wndMain, 5, 30, @width - 10, 20, %scale, %fix)
	do @saveBtn :: wnd@makeBtn(@wndMain, 5, 60, 50, 20, %fix, %fix, "保存")
	do @resetBtn :: wnd@makeBtn(@wndMain, @width - 55, 60, 50, 20, %move, %fix, "リセット")
	do @resetBtn.onPush :: reset
	do @saveBtn.onPush :: save
	do @tabOrder :: wnd@makeTabOrder([@inputEdit $ wnd@WndBase, @saveBtn, @resetBtn])
	do reset(null)
	while(wnd@act())
	end while

	func reset(wnd: wnd@WndBase)
		do @inputEdit.setText(@defaultInputStr)
	end func

	func save(wnd: wnd@WndBase)
		var path: []char :: wnd@saveFileDialog(@wndMain, ["音声データ (*.wav)", "*.wav"], 0, "wav")
		if(path =& null)
			ret
		end if
		var filePtr: file@Writer :: file@makeWriter(path, false)
		if(filePtr =& null)
			do wnd@msgBox(@wndMain, "保存に失敗しました。\{path}", @title, %err, %ok)
			ret
		end if

		var inputStr: []char :: @inputEdit.getText()
		var splittedStrs: [][]char :: inputStr.split(" ")
		var datasize: int :: ^splittedStrs * 44100
		var filesize: int :: datasize + 44 {ヘッダが44bytes}
		var header: []bit8 :: [
		| 16#52b8, 16#49b8, 16#46b8, 16#46b8] {'RIFF'}
		| ~ ((filesize - 8) $> []bit8).sub(0, 4) ~ {ファイルサイズ(byte) - 8(byte)}
		| [
		| 16#57b8, 16#41b8, 16#56b8, 16#45b8, {'WAVE'}
		| 16#66b8, 16#6Db8, 16#74b8, 16#20b8, {'fmt '}
		| 16#10b8, 16#00b8, 16#00b8, 16#00b8, {fmt チャンクのバイト数}
		| 16#01b8, 16#00b8, {フォーマットID}
		| 16#01b8, 16#00b8, {チャンネル数}
		| 16#44b8, 16#ACb8, 16#00b8, 16#00b8, {サンプリングレート}
		| 16#44b8, 16#ACb8, 16#00b8, 16#00b8, {データ速度}
		| 16#01b8, 16#00b8, {ブロックサイズ}
		| 16#08b8, 16#00b8, {サンプルあたりのビット数}
		| 16#64b8, 16#61b8, 16#74b8, 16#61b8] {'data'}
		| ~ ((datasize - 8) $> []bit8).sub(0, 4)  {データサイズ(byte)}
		var bin: []bit8 :: ##header
		var x: float :: 0.0
		for i(0, ^splittedStrs - 1)
			var key: float
			do splittedStrs[i].toFloat(&key)
			for j(0, 44100 / 4)
				var a: int :: (lib@sin(x) * 127.0 + 128.0) $ int
				do bin :~ [a $ bit8]
				do x :+ 441.0 * 2.0 ^ (key / 12.0) * 2.0 * lib@pi / 44100.0
			end for
		end for
		do filePtr.write(bin)
		do filePtr.fin()
		do wnd@msgBox(@wndMain, "保存しました。", @title, %none, %ok)
	end func

	func saveWav(bin: []bit8): bool
	end func
end func
