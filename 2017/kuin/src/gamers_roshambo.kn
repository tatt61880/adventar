var width: float :: 400.0
var height: float :: 450.0
var texP: draw@Tex
var texS: draw@Tex
var texR: draw@Tex
var font: draw@Font
var wndMain: wnd@Wnd 
var drawMain: wnd@Draw
var title: []char :: "ゲーマーじゃんけんの判定をするゲーム"
var selected: @Hand
var answered: bool
var correct: bool

func main()
	do @font :: draw@makeFont(null, 24, true, false, false, 30.0)
	do @texP :: draw@makeTex("res/janken_pa.png")
	do @texS :: draw@makeTex("res/janken_choki.png")
	do @texR :: draw@makeTex("res/janken_gu.png")
	do @handStr :: ["パー", "チョキ", "グー", "あいこ"]
	do @wndMain :: wnd@makeWnd(null, %aspect, @width $ int, @height $ int, @title)
	do @drawMain :: wnd@makeDraw(@wndMain, 0, 0, @width $ int, @height $ int, %scale, %scale, false)
	do @drawMain.onMouseUpL :: @drawMainOnMouseUpL
	do draw@clearColor(16#FF333333)
	do wnd@msgBox(@wndMain, "ゲーマーじゃんけんの勝敗判定を素早く行えるように訓練するゲームです。\nゲーマーじゃんけんは少数派の手が勝ちのルールです。\n少数派の手が複数ある場合はその中でじゃんけんの強い方が勝ちです。\n12人でゲーマーじゃんけんが行われます。勝敗判定を行ってください。", @title, %none, %ok)
	while(@game())
	end while
end func

enum Hand
	paper
	scissors
	rock
	drawn
end enum
var handStr: [][]char

var hands: []@Hand
func game(): bool
	do @hands :: #[12]@Hand
	for i(0, 11)
		do @hands[i] :: lib@rnd(0, 2) $ @Hand
	end for
	while(wnd@act())
		for i(0, 11)
			var x: int :: i % 4
			var y: int :: i / 4
			do @drawHand(@hands[i], 100.0 * x $ float, 100.0 * y $ float)
		end for
		do draw@rect(0.0, 300.0, @width, @height - 300.0, 16#FFFFFFFF)
		do @font.draw(5.0, 300.0, "勝ちの手を選択してください", 16#FF000000)
		do draw@line(0.0, 340.0, @width, 340.0, 16#FFAAAAAA)
		do draw@line(100.0, 340.0, 100.0, @height, 16#FFAAAAAA)
		do draw@line(200.0, 340.0, 200.0, @height, 16#FFAAAAAA)
		do draw@line(300.0, 340.0, 300.0, @height, 16#FFAAAAAA)
		do @drawHand(%rock, 0.0, 345.0)
		do @drawHand(%scissors, 100.0, 345.0)
		do @drawHand(%paper, 200.0, 345.0)
		do @font.draw(305.0, 370.0, "あいこ", 16#FF000000)
		do draw@render(60)
		if(@answered)
			do @answered :: false
			if(@correct)
				ret true
			else
				do wnd@msgBox(@wndMain, "不正解です!\n\{@handStr[@selected $ int]}ではありません！", @title, %none, %ok)
			end if
		end if
	end while
end func

func drawHand(hand: @Hand, x: float, y: float)
	switch(hand)
	case %paper
		do @texP.drawScale(x, y, 100.0, 100.0, 0.0, 0.0, 256.0, 256.0, 16#FFFFFFFF)
	case %scissors
		do @texS.drawScale(x, y, 100.0, 100.0, 0.0, 0.0, 256.0, 256.0, 16#FFFFFFFF)
	case %rock
		do @texR.drawScale(x, y, 100.0, 100.0, 0.0, 0.0, 256.0, 256.0, 16#FFFFFFFF)
	end switch
end func

func drawMainOnMouseUpL(wnd: wnd@Draw, x: int, y: int)
	if(y < 340)
		ret
	end if
	var result: @Hand :: @judge()
	if(x < 100)
		do @selected :: %rock
	elif(x < 200)
		do @selected :: %scissors
	elif(x < 300)
		do @selected :: %paper
	else
		do @selected :: %drawn
	end if
	do @answered :: true
	do @correct :: result = @selected
end func

func judge(): @Hand
	var count_p: int :: 0
	var count_s: int :: 0
	var count_r: int :: 0
	for i(0, ^@hands - 1)
		switch(@hands[i])
		case %paper
			do count_p :+ 1
		case %scissors
			do count_s :+ 1
		case %rock
			do count_r :+ 1
		end switch
	end for
	if(count_p = count_s & count_s = count_r)
		ret %drawn
	end if
	if(count_p >= count_s)
		if(count_s < count_r)
			ret %scissors
		else
			ret %rock
		end if
	else
		if(count_p <= count_r)
			ret %paper
		else
			ret %rock
		end if
	end if
end func
