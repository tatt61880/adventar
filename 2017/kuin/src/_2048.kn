const title: []char :: "2048"
var wndMain: wnd@Wnd 
var drawMain: wnd@Draw
var font: []draw@Font
var fontSizeW: []float
var fontSizeH2: []float
var pieces: [][]int
var colors: []int
var nextFlag: bool
var waitFlag: bool
var clearedFlag: bool
const size: float :: 100.0
const r: float :: 10.0
const s: float :: 4.0
const startNum: int :: 2
const rndMin: int :: 1
const rndMax: int :: 2

func main()
	assert @startNum > 0
	assert @rndMin > 0
	assert @rndMin < 10
	assert @rndMax < 10
	assert @rndMin <= @rndMax
	do @font :: #[21]draw@Font
	do @fontSizeW :: #[21]float
	do @fontSizeH2 :: #[21]float
	do @fontSizeW[1] :: @size / 1.5 / 1.1
	do @fontSizeH2[1] :: @size / 2.0
	do @font[1] :: draw@makeFont(null, @fontSizeH2[1] $ int, true, false, false, @fontSizeW[1])
	for i(2, 20)
		do @fontSizeW[i] :: @size / i $ float / 1.1
		do @fontSizeH2[i] :: @size / i $ float
		do @font[i] :: draw@makeFont(null, (@fontSizeH2[i]) $ int, true, false, false, @fontSizeW[i])
	end for
	do @wndMain :: wnd@makeWnd(null, %aspect, @size $ int * 4, @size $ int * 4, @title)
	do @drawMain :: wnd@makeDraw(@wndMain, 0, 0, @size $ int * 4, @size $ int * 4, %scale, %scale, false)
	do @colors :: [0, 16#FFEEDDDD, 16#FFDDCCAA, 16#FFFFAAAA, 16#FFFF5555, 16#FFFF3333, 16#FFFF1111] ~ [16#FFCCCC44].repeat(20)
	do wnd@onKeyPress(@onKeyPress)
	do draw@clearColor(16#FF333333)
	while(@game())
	end while
end func

func game(): bool
	do @reset()
	while(wnd@act())
		do @waitFlag :: false
		do @drawAll()
		if(@nextFlag)
			do @nextFlag :: false
			do @add()
			if(!@clearedFlag & @isCleared())
				do @drawAll()
				if(wnd@msgBox(@wndMain, "Congratulations!\nContinue?", @title, %question, %yesNo) = %yes)
					do @clearedFlag :: true
				else
					ret false
				end if
			elif(@isGameover())
				do @drawAll()
				ret wnd@msgBox(@wndMain, "Game Over!\nTry again?", @title, %question, %yesNo) = %yes
			end if
		end if
	end while
end func

func drawAll()
	for y(0, 3)
		for x(0, 3)
			var n: int :: @pieces[y][x]
			if(n <> 0)
				do drawPiece(n, x $ float * @size, y $ float * @size)
			end if
		end for
	end for
	func drawPiece(n: int, x: float, y: float)
		var color: int :: @colors[n]
		var fontColor: int :: color % 256 < 128 ?(16#FFEEEEEE, 16#FF111111)
		do draw@rect(x + @s, y + @r + @s, @size - 2.0 * @s, @size - 2.0 * (@r + @s), color)
		do draw@rect(x + @s + @r, y + @s, @size - 2.0 * (@r + @s), @size - 2.0 * @s, color)
		do draw@circle(x + @r + @s, y + @r + @s, @r, @r, color)
		do draw@circle(x + @r + @s, y + @size - @r - @s, @r, @r, color)
		do draw@circle(x + @size - @r - @s, y + @size - @r - @s, @r, @r, color)
		do draw@circle(x + @size - @r - @s, y + @r + @s, @r, @r, color)
		var numStr: []char :: (2 ^ n).toStr()
		do @font[^numStr].draw(x + 0.5 * (@size - @fontSizeW[^numStr] * (^numStr) $ float), y + 0.5 * @size - @fontSizeH2[^numStr], numStr, fontColor)
	end func
	do draw@render(30)
end func

func reset()
	do @clearedFlag :: false
	do @nextFlag :: false
	do @waitFlag :: false
	do @pieces :: #[4, 4]int
	for(1, @startNum)
		do @add()
	end for
end func

func add()
	var xy: [][]int :: #[0][]int
	for y(0, 3)
		for x(0, 3)
			if(@pieces[y][x] = 0)
				do xy :~ [[x, y]]
			end if
		end for
	end for
	if(^xy > 0)
		var rnd: int :: lib@rnd(0, ^xy - 1)
		do @pieces[xy[rnd][1]][xy[rnd][0]] :: lib@rnd(@rndMin, @rndMax)
	end if
end func

func onKeyPress(key: wnd@Key, shiftCtrl: wnd@ShiftCtrl): bool
	if(@waitFlag)
		ret false
	end if
	do @waitFlag :: true
	switch(key)
	case %up
		for x(0, 3)
			do @move(&@pieces[0][x], &@pieces[1][x], &@pieces[2][x], &@pieces[3][x])
		end for
	case %down
		for x(0, 3)
			do @move(&@pieces[3][x], &@pieces[2][x], &@pieces[1][x], &@pieces[0][x])
		end for
	case %left
		for y(0, 3)
			do @move(&@pieces[y][0], &@pieces[y][1], &@pieces[y][2], &@pieces[y][3])
		end for
	case %right
		for y(0, 3)
			do @move(&@pieces[y][3], &@pieces[y][2], &@pieces[y][1], &@pieces[y][0])
		end for
	end switch
	ret false
end func

func move(a: &int, b: &int, c: &int, d: &int)
	var arrPrev: []int :: [a, b, c, d]
	var arrNext: []int :: [0]
	for i(0, 3)
		if(arrPrev[i] = 0)
			skip i
		end if
		if(arrNext[^arrNext - 1] = 0)
			do arrNext[^arrNext - 1] :: arrPrev[i]
		elif(arrNext[^arrNext - 1] = arrPrev[i])
			do arrNext[^arrNext - 1] :+ 1
			do arrNext :~ [0]
		else
			do arrNext :~ [arrPrev[i]]
		end if
	end for
	do arrNext :~ [0].repeat(4 - ^arrNext)
	for i(0, 3)
		if(arrPrev[i] <> arrNext[i])
			do @nextFlag :: true
		end if
	end for
	do a :: arrNext[0]
	do b :: arrNext[1]
	do c :: arrNext[2]
	do d :: arrNext[3]
end func

func isGameover(): bool
	for y(0, 3)
		for x(0, 3)
			if(@pieces[y][x] = 0)
				ret false
			end if
		end for
	end for
	for y(0, 3)
		for x(0, 2)
			if(@pieces[y][x] = @pieces[y][x + 1])
				ret false
			end if
		end for
	end for
	for y(0, 2)
		for x(0, 3)
			if(@pieces[y][x] = @pieces[y + 1][x])
				ret false
			end if
		end for
	end for
	ret true
end func

func isCleared(): bool
	for y(0, 3)
		for x(0, 3)
			if(@pieces[y][x] > 10)
				ret true
			end if
		end for
	end for
	ret false
end func
